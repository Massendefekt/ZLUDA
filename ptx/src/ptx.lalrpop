use crate::{without_none, vector_index};

use crate::ast;
use crate::ast::UnwrapWithVec;
use crate::instructions::arguments;
use crate::error;
use crate::function;
use crate::instructions;
use crate::kernel;
use crate::statement;
use crate::types;
use crate::variable;

use lalrpop_util::ParseError;
use std::convert::TryInto;

grammar<'a>(errors: &mut Vec<error::PtxError>);

extern {
    type Error = error::PtxError;
}

match {
    r"\s+" => { },
    r"//[^\n\r]*[\n\r]*" => { },
    r"/\*([^\*]*\*+[^\*/])*([^\*]*\*+|[^\*])*\*/" => { },
    r"0[fF][0-9a-zA-Z]{8}" => F32NumToken,
    r"0[dD][0-9a-zA-Z]{16}" => F64NumToken,
    r"0[xX][0-9a-zA-Z]+U?" => HexNumToken,
    r"[0-9]+U?" => DecimalNumToken,
    r#""[^"]*""# => String,
    r"[0-9]+\.[0-9]+" => VersionNumber,
    "!",
    "(", ")",
    "+",
    "-",
    ",",
    ".",
    ":",
    ";",
    "@",
    "[", "]",
    "{", "}",
    "<", ">",
    "|",
    "=",
    ".acq_rel",
    ".acquire",
    ".add",
    ".address_size",
    ".align",
    ".aligned",
    ".and",
    ".approx",
    ".b16",
    ".b32",
    ".b64",
    ".b8",
    ".ca",
    ".cas",
    ".cg",
    ".const",
    ".cs",
    ".cta",
    ".cv",
    ".dec",
    ".entry",
    ".eq",
    ".equ",
    ".exch",
    ".extern",
    ".f16",
    ".f16x2",
    ".f32",
    ".f64",
    ".file",
    ".ftz",
    ".full",
    ".func",
    ".ge",
    ".geu",
    ".global",
    ".gpu",
    ".gt",
    ".gtu",
    ".hi",
    ".hs",
    ".inc",
    ".le",
    ".leu",
    ".lo",
    ".loc",
    ".local",
    ".ls",
    ".lt",
    ".ltu",
    ".lu",
    ".max",
    ".min",
    ".nan",
    ".NaN",
    ".ne",
    ".neu",
    ".num",
    ".or",
    ".param",
    ".pragma",
    ".pred",
    ".reg",
    ".relaxed",
    ".release",
    ".rm",
    ".rmi",
    ".rn",
    ".rni",
    ".rp",
    ".rpi",
    ".rz",
    ".rzi",
    ".s16",
    ".s32",
    ".s64",
    ".s8" ,
    ".sat",
    ".section",
    ".shared",
    ".sync",
    ".sys",
    ".target",
    ".to",
    ".u16",
    ".u32",
    ".u64",
    ".u8" ,
    ".uni",
    ".v2",
    ".v4",
    ".version",
    ".visible",
    ".volatile",
    ".wb",
    ".weak",
    ".wide",
    ".wt",
    ".xor",
} else {
    // IF YOU ARE ADDING A NEW TOKEN HERE ALSO ADD IT BELOW TO ExtendedID
    "abs",
    "add",
    "and",
    "atom",
    "bar",
    "barrier",
    "bfe",
    "bra",
    "brev",
    "call",
    "clz",
    "cos",
    "cvt",
    "cvta",
    "debug",
    "div",
    "ex2",
    "fma",
    "ld",
    "lg2",
    "mad",
    "map_f64_to_f32",
    "max",
    "min",
    "mov",
    "mul",
    "neg",
    "not",
    "or",
    "popc",
    "rcp",
    "rem",
    "ret",
    "rsqrt",
    "selp",
    "setp",
    "shl",
    "shr",
    "sin",
    r"sm_[0-9]+" => ShaderModel,
    "sqrt",
    "st",
    "sub",
    "texmode_independent",
    "texmode_unified",
    "xor",
} else {
    // https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#identifiers
    r"[a-zA-Z][a-zA-Z0-9_$]*|[_$%][a-zA-Z0-9_$]+" => ID,
    r"\.[a-zA-Z][a-zA-Z0-9_$]*" => DotID,
}

ExtendedID : &'input str = {
    "abs",
    "add",
    "and",
    "atom",
    "bar",
    "barrier",
    "bfe",
    "bra",
    "brev",
    "call",
    "clz",
    "cos",
    "cvt",
    "cvta",
    "debug",
    "div",
    "ex2",
    "fma",
    "ld",
    "lg2",
    "mad",
    "map_f64_to_f32",
    "max",
    "min",
    "mov",
    "mul",
    "neg",
    "not",
    "or",
    "popc",
    "rcp",
    "rem",
    "ret",
    "rsqrt",
    "selp",
    "setp",
    "shl",
    "shr",
    "sin",
    ShaderModel,
    "sqrt",
    "st",
    "sub",
    "texmode_independent",
    "texmode_unified",
    "xor",
    ID
}

NumToken: (&'input str, u32, bool) = {
    <s:HexNumToken> => {
        if s.ends_with('U') {
            (&s[2..s.len() - 1], 16, true)
        } else {
            (&s[2..], 16, false)
        }
    },
    <s:DecimalNumToken> => {
        let radix = if s.starts_with('0') { 8 } else { 10 };
        if s.ends_with('U') {
            (&s[..s.len() - 1], radix, true)
        } else {
            (s, radix, false)
        }
    }
}

F32Num: f32 = {
    <s:F32NumToken> =>? {
        match u32::from_str_radix(&s[2..], 16) {
            Ok(x) => Ok(unsafe { std::mem::transmute::<_, f32>(x) }),
            Err(err) => Err(ParseError::User { error: error::PtxError::from(err) })
        }
    }
}

F64Num: f64 = {
    <s:F64NumToken> =>? {
        match u64::from_str_radix(&s[2..], 16) {
            Ok(x) => Ok(unsafe { std::mem::transmute::<_, f64>(x) }),
            Err(err) => Err(ParseError::User { error: error::PtxError::from(err) })
        }
    }
}

U8Num: u8 = {
    <x:NumToken> =>? {
        let (text, radix, _) = x;
        match u8::from_str_radix(text, radix) {
            Ok(x) => Ok(x),
            Err(err) => Err(ParseError::User { error: error::PtxError::from(err) })
        }
    }
}

U32Num: u32 = {
    <x:NumToken> =>? {
        let (text, radix, _) = x;
        match u32::from_str_radix(text, radix) {
            Ok(x) => Ok(x),
            Err(err) => Err(ParseError::User { error: error::PtxError::from(err) })
        }
    }
}

// TODO: handle negative number properly
S32Num: i32 = {
    <sign:"-"?> <x:NumToken> =>? {
        let (text, radix, _) = x;
        match i32::from_str_radix(text, radix) {
            Ok(x) => Ok(if sign.is_some() { -x } else { x }),
            Err(err) => Err(ParseError::User { error: error::PtxError::from(err) })
        }
    }
}

pub Module: crate::Module<'input> = {
    <v:Version> Target <d:Directive*> => {
        crate::Module { version: v, directives: without_none(d) }
    }
};

Version: (u8, u8) = {
    ".version" <v:VersionNumber> => {
        let dot = v.find('.').unwrap();
        let major = v[..dot].parse::<u8>();
        let minor = v[dot+1..].parse::<u8>();
        (major,minor).unwrap_with(errors)
    }
}

// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#ptx-module-directives-target
Target = {
    ".target" Comma<TargetSpecifier>
};

TargetSpecifier = {
    ShaderModel,
    "texmode_unified",
    "texmode_independent",
    "debug",
    "map_f64_to_f32"
};

Directive: Option<crate::Directive<'input, instructions::arguments::ParsedArgParams<'input>>> = {
    AddressSize => None,
    <f:Function> => Some(crate::Directive::Method(f)),
    File => None,
    Section => None,
    <v:ModuleVariable> ";" => Some(crate::Directive::Variable(v)),
};

AddressSize = {
    ".address_size" U8Num
};

Function: function::Function<'input, &'input str, statement::Statement<instructions::arguments::ParsedArgParams<'input>>>  = {
    LinkingDirectives
    <func_directive:MethodDecl>
    <body:FunctionBody> =>  function::Function{<>}
};

LinkingDirective: ast::LinkingDirective = {
    ".extern" => ast::LinkingDirective::EXTERN,
    ".visible" => ast::LinkingDirective::VISIBLE,
    ".weak" => ast::LinkingDirective::WEAK,
};

LinkingDirectives: ast::LinkingDirective = {
    <ldirs:LinkingDirective*> => {
        ldirs.into_iter().fold(ast::LinkingDirective::NONE, |x, y| x | y)
    }
}

MethodDecl: function::MethodDecl<'input, &'input str> = {
    ".entry" <name:ExtendedID> <in_args:KernelArguments> =>
        function::MethodDecl::Kernel{ name, in_args },
    ".func" <ret_vals:FnArguments?> <name:ExtendedID> <params:FnArguments> => {
        function::MethodDecl::Func(ret_vals.unwrap_or_else(|| Vec::new()), name, params)
    }
};

KernelArguments: Vec<kernel::KernelArgument<&'input str>> = {
    "(" <args:Comma<KernelInput>> ")" => args
};

FnArguments: Vec<function::FnArgument<&'input str>> = {
    "(" <args:Comma<FnInput>> ")" => args
};

KernelInput: variable::Variable<kernel::KernelArgumentType, &'input str> = {
    <v:ParamDeclaration> => {
        let (align, v_type, name) = v;
        variable::Variable {
            align,
            v_type: kernel::KernelArgumentType::Normal(v_type),
            name,
            array_init: Vec::new()
        }
    }
}

FnInput: variable::Variable<function::FnArgumentType, &'input str> = {
    <v:RegVariable> => {
        let (align, v_type, name) = v;
        let v_type = function::FnArgumentType::Reg(v_type);
        variable::Variable{ align, v_type, name, array_init: Vec::new() }
    },
    <v:ParamDeclaration> => {
        let (align, v_type, name) = v;
        let v_type = function::FnArgumentType::Param(v_type);
        variable::Variable{ align, v_type, name, array_init: Vec::new() }
    }
}

pub(crate) FunctionBody: Option<Vec<statement::Statement<instructions::arguments::ParsedArgParams<'input>>>> = {
    "{" <s:Statement*> "}" => { Some(without_none(s)) },
    ";" => { None }
};

StateSpaceSpecifier: variable::StateSpace = {
    ".reg" => variable::StateSpace::Reg,
    ".const" => variable::StateSpace::Const,
    ".global" => variable::StateSpace::Global,
    ".local" => variable::StateSpace::Local,
    ".shared" => variable::StateSpace::Shared,
    ".param" => variable::StateSpace::Param, // used to prepare function call
};

#[inline]
ScalarType: types::scalar::ScalarType = {
    ".f16" => types::scalar::ScalarType::F16,
    ".f16x2" => types::scalar::ScalarType::F16x2,
    ".pred" => types::scalar::ScalarType::Pred,
    ".b8" => types::scalar::ScalarType::B8,
    ".b16" => types::scalar::ScalarType::B16,
    ".b32" => types::scalar::ScalarType::B32,
    ".b64" => types::scalar::ScalarType::B64,
    ".u8" => types::scalar::ScalarType::U8,
    ".u16" => types::scalar::ScalarType::U16,
    ".u32" => types::scalar::ScalarType::U32,
    ".u64" => types::scalar::ScalarType::U64,
    ".s8" => types::scalar::ScalarType::S8,
    ".s16" => types::scalar::ScalarType::S16,
    ".s32" => types::scalar::ScalarType::S32,
    ".s64" => types::scalar::ScalarType::S64,
    ".f32" => types::scalar::ScalarType::F32,
    ".f64" => types::scalar::ScalarType::F64,
};

Statement: Option<statement::Statement<instructions::arguments::ParsedArgParams<'input>>> = {
    <l:Label> => Some(statement::Statement::Label(l)),
    DebugDirective => None,
    <v:MultiVariable> ";" => Some(statement::Statement::Variable(v)),
    <p:PredAt?> <i:instructions> ";" => Some(statement::Statement::Instruction(p, i)),
    PragmaStatement => None,
    "{" <s:Statement*> "}" => Some(statement::Statement::Block(without_none(s)))
};

PragmaStatement: () = {
    ".pragma" String  ";"
}

DebugDirective: () = {
    DebugLocation
};

// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#debugging-directives-loc
DebugLocation = {
    ".loc" U32Num U32Num U32Num
};

Label: &'input str = {
    <id:ExtendedID> ":" => id
};

Align: u32 = {
    ".align" <x:U32Num> => x
};

// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#parameterized-variable-names
MultiVariable: variable::MultiVariable<&'input str> = {
    <var:Variable> <count:VariableParam?> => variable::MultiVariable{<>}
}

VariableParam: u32 = {
    "<" <n:U32Num> ">" => n
}

Variable: variable::Variable<variable::VariableType, &'input str> = {
    <v:RegVariable> => {
        let (align, v_type, name) = v;
        let v_type = variable::VariableType::Reg(v_type);
        variable::Variable {align, v_type, name, array_init: Vec::new()}
    },
    LocalVariable,
    <v:ParamVariable> => {
        let (align, array_init, v_type, name) = v;
        let v_type = variable::VariableType::Param(v_type);
        variable::Variable {align, v_type, name, array_init}
    },
    SharedVariable,
};

RegVariable: (Option<u32>, variable::VariableRegType, &'input str) = {
    ".reg" <var:VariableScalar<ScalarType>> => {
        let (align, t, name) = var;
        let v_type = variable::VariableRegType::Scalar(t);
        (align, v_type, name)
    },
    ".reg" <var:VariableVector<SizedScalarType>> => {
        let (align, v_len, t, name) = var;
        let v_type = variable::VariableRegType::Vector(t, v_len);
        (align, v_type, name)
    }
}

LocalVariable: variable::Variable<variable::VariableType, &'input str> = {
    ".local" <var:VariableScalar<SizedScalarType>> => {
        let (align, t, name) = var;
        let v_type = variable::VariableType::Local(variable::VariableLocalType::Scalar(t));
        variable::Variable { align, v_type, name, array_init: Vec::new() }
    },
    ".local" <var:VariableVector<SizedScalarType>> => {
        let (align, v_len, t, name) = var;
        let v_type = variable::VariableType::Local(variable::VariableLocalType::Vector(t, v_len));
        variable::Variable { align, v_type, name, array_init: Vec::new() }
    },
    ".local" <var:VariableArrayOrPointer<SizedScalarType>> =>? {
        let (align, t, name, arr_or_ptr) = var;
        let (v_type, array_init) = match arr_or_ptr {
            ast::ArrayOrPointer::Array { dimensions, init } => {
                (variable::VariableLocalType::Array(t, dimensions), init)
            }
            ast::ArrayOrPointer::Pointer => {
                return Err(ParseError::User { error: error::PtxError::ZeroDimensionArray });
            }
        };
        Ok(variable::Variable { align, v_type: variable::VariableType::Local(v_type), name, array_init })
    }
}

SharedVariable: variable::Variable<variable::VariableType, &'input str> = {
    ".shared" <var:VariableScalar<SizedScalarType>> => {
        let (align, t, name) = var;
        let v_type = variable::VariableGlobalType::Scalar(t);
        variable::Variable { align, v_type: variable::VariableType::Shared(v_type), name, array_init: Vec::new() }
    },
    ".shared" <var:VariableVector<SizedScalarType>> => {
        let (align, v_len, t, name) = var;
        let v_type = variable::VariableGlobalType::Vector(t, v_len);
        variable::Variable { align, v_type: variable::VariableType::Shared(v_type), name, array_init: Vec::new() }
    },
    ".shared" <var:VariableArrayOrPointer<SizedScalarType>> =>? {
        let (align, t, name, arr_or_ptr) = var;
        let (v_type, array_init) = match arr_or_ptr {
            ast::ArrayOrPointer::Array { dimensions, init } => {
                (variable::VariableGlobalType::Array(t, dimensions), init)
            }
            ast::ArrayOrPointer::Pointer => {
                return Err(ParseError::User { error: error::PtxError::ZeroDimensionArray });
            }
        };
        Ok(variable::Variable { align, v_type: variable::VariableType::Shared(v_type), name, array_init })
    }
}


ModuleVariable: variable::Variable<variable::VariableType, &'input str> = {
    LinkingDirectives ".global" <def:GlobalVariableDefinitionNoArray> => {
        let (align, v_type, name, array_init) = def;
        variable::Variable { align, v_type: variable::VariableType::Global(v_type), name, array_init }
    },
    LinkingDirectives ".shared" <def:GlobalVariableDefinitionNoArray> => {
        let (align, v_type, name, array_init) = def;
        variable::Variable { align, v_type: variable::VariableType::Shared(v_type), name, array_init: Vec::new() }
    },
    <ldirs:LinkingDirectives> <space:Or<".global", ".shared">> <var:VariableArrayOrPointer<SizedScalarType>> =>? {
        let (align, t, name, arr_or_ptr) = var;
        let (v_type, array_init) = match arr_or_ptr {
            ast::ArrayOrPointer::Array { dimensions, init } => {
                if space == ".global" {
                    (variable::VariableType::Global(variable::VariableGlobalType::Array(t, dimensions)), init)
                } else {
                    (variable::VariableType::Shared(variable::VariableGlobalType::Array(t, dimensions)), init)
                }
            }
            ast::ArrayOrPointer::Pointer => {
                if !ldirs.contains(ast::LinkingDirective::EXTERN) {
                    return Err(ParseError::User { error: error::PtxError::NonExternPointer });
                }
                if space == ".global" {
                    (variable::VariableType::Global(variable::VariableGlobalType::Pointer(t, types::pointer::PointerStateSpace::Global)), Vec::new())
                } else {
                    (variable::VariableType::Shared(variable::VariableGlobalType::Pointer(t, types::pointer::PointerStateSpace::Shared)), Vec::new())
                }
            }
        };
        Ok(variable::Variable{ align, array_init, v_type, name })
    }
}

// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#parameter-state-space
ParamVariable: (Option<u32>, Vec<u8>, variable::VariableParamType, &'input str) = {
    ".param" <var:VariableScalar<LdStScalarType>> => {
        let (align, t, name) = var;
        let v_type = variable::VariableParamType::Scalar(t);
        (align, Vec::new(), v_type, name)
    },
    ".param" <var:VariableArrayOrPointer<SizedScalarType>> => {
        let (align, t, name, arr_or_ptr) = var;
        let (v_type, array_init) = match arr_or_ptr {
            ast::ArrayOrPointer::Array { dimensions, init } => {
                (variable::VariableParamType::Array(t, dimensions), init)
            }
            ast::ArrayOrPointer::Pointer => {
                (variable::VariableParamType::Pointer(t, types::pointer::PointerStateSpace::Param), Vec::new())
            }
        };
        (align, array_init, v_type, name)
    }
}

ParamDeclaration: (Option<u32>, variable::VariableParamType, &'input str) = {
    <var:ParamVariable> =>? {
        let (align, array_init, v_type, name) = var;
        if array_init.len() > 0 {
            Err(ParseError::User { error: error::PtxError::ArrayInitalizer })
        } else {
            Ok((align, v_type, name))
        }
    }
}

GlobalVariableDefinitionNoArray: (Option<u32>, variable::VariableGlobalType, &'input str, Vec<u8>) = {
    <scalar:VariableScalar<SizedScalarType>> => {
        let (align, t, name) = scalar;
        let v_type = variable::VariableGlobalType::Scalar(t);
        (align, v_type, name, Vec::new())
    },
    <var:VariableVector<SizedScalarType>> => {
        let (align, v_len, t, name) = var;
        let v_type = variable::VariableGlobalType::Vector(t, v_len);
        (align, v_type, name, Vec::new())
    },
}

#[inline]
SizedScalarType: types::scalar::SizedScalarType = {
    ".b8" => types::scalar::SizedScalarType::B8,
    ".b16" => types::scalar::SizedScalarType::B16,
    ".b32" => types::scalar::SizedScalarType::B32,
    ".b64" => types::scalar::SizedScalarType::B64,
    ".u8" => types::scalar::SizedScalarType::U8,
    ".u16" => types::scalar::SizedScalarType::U16,
    ".u32" => types::scalar::SizedScalarType::U32,
    ".u64" => types::scalar::SizedScalarType::U64,
    ".s8" => types::scalar::SizedScalarType::S8,
    ".s16" => types::scalar::SizedScalarType::S16,
    ".s32" => types::scalar::SizedScalarType::S32,
    ".s64" => types::scalar::SizedScalarType::S64,
    ".f16" => types::scalar::SizedScalarType::F16,
    ".f16x2" => types::scalar::SizedScalarType::F16x2,
    ".f32" => types::scalar::SizedScalarType::F32,
    ".f64" => types::scalar::SizedScalarType::F64,
}

#[inline]
LdStScalarType: types::scalar::LdStScalarType = {
    ".b8" => types::scalar::LdStScalarType::B8,
    ".b16" => types::scalar::LdStScalarType::B16,
    ".b32" => types::scalar::LdStScalarType::B32,
    ".b64" => types::scalar::LdStScalarType::B64,
    ".u8" => types::scalar::LdStScalarType::U8,
    ".u16" => types::scalar::LdStScalarType::U16,
    ".u32" => types::scalar::LdStScalarType::U32,
    ".u64" => types::scalar::LdStScalarType::U64,
    ".s8" => types::scalar::LdStScalarType::S8,
    ".s16" => types::scalar::LdStScalarType::S16,
    ".s32" => types::scalar::LdStScalarType::S32,
    ".s64" => types::scalar::LdStScalarType::S64,
    ".f16" => types::scalar::LdStScalarType::F16,
    ".f32" => types::scalar::LdStScalarType::F32,
    ".f64" => types::scalar::LdStScalarType::F64,
}

instructions: instructions::Instruction<instructions::arguments::ParsedArgParams<'input>> = {
    InstLd,
    InstMov,
    InstMul,
    InstAdd,
    InstSetp,
    InstNot,
    InstBra,
    InstCvt,
    InstShl,
    InstShr,
    InstSt,
    InstRet,
    InstCvta,
    InstCall,
    InstAbs,
    InstMad,
    InstOr,
    InstAnd,
    InstSub,
    InstMin,
    InstMax,
    InstRcp,
    InstSelp,
    InstBar,
    InstAtom,
    InstAtomCas,
    InstDiv,
    InstSqrt,
    InstRsqrt,
    InstNeg,
    InstSin,
    InstCos,
    InstLg2,
    InstEx2,
    InstClz,
    InstBrev,
    InstPopc,
    InstXor,
    InstRem,
    InstBfe,
};

// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#data-movement-and-conversion-instructions-ld
InstLd: instructions::Instruction<instructions::arguments::ParsedArgParams<'input>> = {
    "ld" <q:LdStQualifier?> <ss:LdStateSpace?> <cop:LdCacheOperator?> <t:LdStType> <dst:DstOperandVec> "," <src:MemoryOperand> => {
        instructions::Instruction::Ld(
            instructions::ld::LdDetails {
                qualifier: q.unwrap_or(instructions::ld::LdStQualifier::Weak),
                state_space: ss.unwrap_or(instructions::ld::LdStateSpace::Generic),
                caching: cop.unwrap_or(instructions::ld::LdCacheOperator::Cached),
                typ: t
            },
            instructions::arguments::Arg2Ld { dst:dst, src:src }
        )
    }
};

LdStType: instructions::ld::LdStType = {
    <v:VectorPrefix> <t:LdStScalarType> => instructions::ld::LdStType::Vector(t, v),
    <t:LdStScalarType> => instructions::ld::LdStType::Scalar(t),
}

LdStQualifier: instructions::ld::LdStQualifier = {
    ".weak" => instructions::ld::LdStQualifier::Weak,
    ".volatile" => instructions::ld::LdStQualifier::Volatile,
    ".relaxed" <s:MemScope> => instructions::ld::LdStQualifier::Relaxed(s),
    ".acquire" <s:MemScope> => instructions::ld::LdStQualifier::Acquire(s),
};

MemScope: instructions::ld::MemScope = {
    ".cta" => instructions::ld::MemScope::Cta,
    ".gpu" => instructions::ld::MemScope::Gpu,
    ".sys" => instructions::ld::MemScope::Sys
};

LdStateSpace: instructions::ld::LdStateSpace = {
    ".const" => instructions::ld::LdStateSpace::Const,
    ".global" => instructions::ld::LdStateSpace::Global,
    ".local" => instructions::ld::LdStateSpace::Local,
    ".param" => instructions::ld::LdStateSpace::Param,
    ".shared" => instructions::ld::LdStateSpace::Shared,
};

LdCacheOperator: instructions::ld::LdCacheOperator = {
    ".ca" => instructions::ld::LdCacheOperator::Cached,
    ".cg" => instructions::ld::LdCacheOperator::L2Only,
    ".cs" => instructions::ld::LdCacheOperator::Streaming,
    ".lu" => instructions::ld::LdCacheOperator::LastUse,
    ".cv" => instructions::ld::LdCacheOperator::Uncached,
};

// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#data-movement-and-conversion-instructions-mov
InstMov: instructions::Instruction<instructions::arguments::ParsedArgParams<'input>> = {
    "mov" <pref:VectorPrefix?> <t:MovScalarType> <dst:DstOperandVec> "," <src:SrcOperandVec> => {
        let mov_type = match pref {
            Some(vec_width) => types::Type::Vector(t, vec_width),
            None => types::Type::Scalar(t)
        };
        let details = instructions::mov::MovDetails::new(mov_type);
        instructions::Instruction::Mov(
            details,
            instructions::arguments::Arg2Mov { dst, src }
        )
    }
}

#[inline]
MovScalarType: types::scalar::ScalarType = {
    ".b16" => types::scalar::ScalarType::B16,
    ".b32" => types::scalar::ScalarType::B32,
    ".b64" => types::scalar::ScalarType::B64,
    ".u16" => types::scalar::ScalarType::U16,
    ".u32" => types::scalar::ScalarType::U32,
    ".u64" => types::scalar::ScalarType::U64,
    ".s16" => types::scalar::ScalarType::S16,
    ".s32" => types::scalar::ScalarType::S32,
    ".s64" => types::scalar::ScalarType::S64,
    ".f32" => types::scalar::ScalarType::F32,
    ".f64" => types::scalar::ScalarType::F64,
    ".pred" => types::scalar::ScalarType::Pred
};

// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#integer-arithmetic-instructions-mul
// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#floating-point-instructions-mul
// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#half-precision-floating-point-instructions-mul
InstMul: instructions::Instruction<instructions::arguments::ParsedArgParams<'input>> = {
    "mul" <d:MulDetails> <a:Arg3> => instructions::Instruction::Mul(d, a)
};

MulDetails: instructions::mul::MulDetails = {
    <ctr:MulIntControl> <t:UIntType> => instructions::mul::MulDetails::Unsigned(instructions::mul::MulUInt{
        typ: t,
        control: ctr
    }),
    <ctr:MulIntControl> <t:SIntType> => instructions::mul::MulDetails::Signed(instructions::mul::MulSInt{
        typ: t,
        control: ctr
    }),
    <f:ArithFloat> => instructions::mul::MulDetails::Float(f)
};

MulIntControl: instructions::mul::MulIntControl = {
    ".hi" => instructions::mul::MulIntControl::High,
    ".lo" => instructions::mul::MulIntControl::Low,
    ".wide" => instructions::mul::MulIntControl::Wide
};

#[inline]
RoundingModeFloat : instructions::rcp::RoundingMode = {
    ".rn" => instructions::rcp::RoundingMode::NearestEven,
    ".rz" => instructions::rcp::RoundingMode::Zero,
    ".rm" => instructions::rcp::RoundingMode::NegativeInf,
    ".rp" => instructions::rcp::RoundingMode::PositiveInf,
};

RoundingModeInt : instructions::rcp::RoundingMode = {
    ".rni" => instructions::rcp::RoundingMode::NearestEven,
    ".rzi" => instructions::rcp::RoundingMode::Zero,
    ".rmi" => instructions::rcp::RoundingMode::NegativeInf,
    ".rpi" => instructions::rcp::RoundingMode::PositiveInf,
};

IntType : types::scalar::IntType = {
    ".u16" => types::scalar::IntType::U16,
    ".u32" => types::scalar::IntType::U32,
    ".u64" => types::scalar::IntType::U64,
    ".s16" => types::scalar::IntType::S16,
    ".s32" => types::scalar::IntType::S32,
    ".s64" => types::scalar::IntType::S64,
};

IntType3264: types::scalar::IntType = {
    ".u32" => types::scalar::IntType::U32,
    ".u64" => types::scalar::IntType::U64,
    ".s32" => types::scalar::IntType::S32,
    ".s64" => types::scalar::IntType::S64,
}

UIntType: types::scalar::UIntType = {
    ".u16" => types::scalar::UIntType::U16,
    ".u32" => types::scalar::UIntType::U32,
    ".u64" => types::scalar::UIntType::U64,
};

SIntType: types::scalar::SIntType = {
    ".s16" => types::scalar::SIntType::S16,
    ".s32" => types::scalar::SIntType::S32,
    ".s64" => types::scalar::SIntType::S64,
};

FloatType: types::scalar::FloatType = {
    ".f16" => types::scalar::FloatType::F16,
    ".f16x2" => types::scalar::FloatType::F16x2,
    ".f32" => types::scalar::FloatType::F32,
    ".f64" => types::scalar::FloatType::F64,
};

// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#integer-arithmetic-instructions-add
// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#floating-point-instructions-add
// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#half-precision-floating-point-instructions-add
InstAdd: instructions::Instruction<instructions::arguments::ParsedArgParams<'input>> = {
    "add" <d:ArithDetails> <a:Arg3> => instructions::Instruction::Add(d, a)
};

// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#comparison-and-selection-instructions-setp
// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#half-precision-comparison-instructions-setp
// TODO: support f16 setp
InstSetp: instructions::Instruction<instructions::arguments::ParsedArgParams<'input>> = {
    "setp" <d:SetpMode> <a:Arg4Setp> => instructions::Instruction::Setp(d, a),
    "setp" <d:SetpBoolMode> <a:Arg5Setp> => instructions::Instruction::SetpBool(d, a),
};

SetpMode: instructions::setp::SetpData = {
    <cmp_op:SetpCompareOp> <t:SetpTypeNoF32> => instructions::setp::SetpData {
        typ: t,
        flush_to_zero: None,
        cmp_op: cmp_op,
    },
    <cmp_op:SetpCompareOp> <ftz:".ftz"?> ".f32" => instructions::setp::SetpData {
        typ: types::scalar::ScalarType::F32,
        flush_to_zero: Some(ftz.is_some()),
        cmp_op: cmp_op,
    }

};

SetpBoolMode: instructions::setp_bool::SetpBoolData = {
    <cmp_op:SetpCompareOp> <bool_op:SetpBoolPostOp> <t:SetpTypeNoF32> => instructions::setp_bool::SetpBoolData {
        typ: t,
        flush_to_zero: None,
        cmp_op: cmp_op,
        bool_op: bool_op,
    },
    <cmp_op:SetpCompareOp> <bool_op:SetpBoolPostOp> <ftz:".ftz"?> ".f32" => instructions::setp_bool::SetpBoolData {
        typ: types::scalar::ScalarType::F32,
        flush_to_zero: Some(ftz.is_some()),
        cmp_op: cmp_op,
        bool_op: bool_op,
    }
};

SetpCompareOp: instructions::setp::SetpCompareOp = {
    ".eq" => instructions::setp::SetpCompareOp::Eq,
    ".ne" => instructions::setp::SetpCompareOp::NotEq,
    ".lt" => instructions::setp::SetpCompareOp::Less,
    ".le" => instructions::setp::SetpCompareOp::LessOrEq,
    ".gt" => instructions::setp::SetpCompareOp::Greater,
    ".ge" => instructions::setp::SetpCompareOp::GreaterOrEq,
    ".lo" => instructions::setp::SetpCompareOp::Less,
    ".ls" => instructions::setp::SetpCompareOp::LessOrEq,
    ".hi" => instructions::setp::SetpCompareOp::Greater,
    ".hs" => instructions::setp::SetpCompareOp::GreaterOrEq,
    ".equ" => instructions::setp::SetpCompareOp::NanEq,
    ".neu" => instructions::setp::SetpCompareOp::NanNotEq,
    ".ltu" => instructions::setp::SetpCompareOp::NanLess,
    ".leu" => instructions::setp::SetpCompareOp::NanLessOrEq,
    ".gtu" => instructions::setp::SetpCompareOp::NanGreater,
    ".geu" => instructions::setp::SetpCompareOp::NanGreaterOrEq,
    ".num" => instructions::setp::SetpCompareOp::IsNotNan,
    ".nan" => instructions::setp::SetpCompareOp::IsNan,
};

SetpBoolPostOp: instructions::setp_bool::SetpBoolPostOp = {
    ".and" => instructions::setp_bool::SetpBoolPostOp::And,
    ".or" => instructions::setp_bool::SetpBoolPostOp::Or,
    ".xor" => instructions::setp_bool::SetpBoolPostOp::Xor,
};

SetpTypeNoF32: types::scalar::ScalarType = {
    ".b16" => types::scalar::ScalarType::B16,
    ".b32" => types::scalar::ScalarType::B32,
    ".b64" => types::scalar::ScalarType::B64,
    ".u16" => types::scalar::ScalarType::U16,
    ".u32" => types::scalar::ScalarType::U32,
    ".u64" => types::scalar::ScalarType::U64,
    ".s16" => types::scalar::ScalarType::S16,
    ".s32" => types::scalar::ScalarType::S32,
    ".s64" => types::scalar::ScalarType::S64,
    ".f64" => types::scalar::ScalarType::F64,
};

// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#logic-and-shift-instructions-not
InstNot: instructions::Instruction<instructions::arguments::ParsedArgParams<'input>> = {
    "not" <t:BooleanType> <a:Arg2> => instructions::Instruction::Not(t, a)
};

BooleanType: types::scalar::BooleanType = {
    ".pred" => types::scalar::BooleanType::Pred,
    ".b16" => types::scalar::BooleanType::B16,
    ".b32" => types::scalar::BooleanType::B32,
    ".b64" => types::scalar::BooleanType::B64,
};

// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#control-flow-instructions-at
PredAt: statement::PredAt<&'input str> = {
    "@" <label:ExtendedID> => statement::PredAt { not: false, label:label },
    "@" "!" <label:ExtendedID> => statement::PredAt { not: true, label:label }
};

// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#control-flow-instructions-bra
InstBra: instructions::Instruction<instructions::arguments::ParsedArgParams<'input>> = {
    "bra" <u:".uni"?> <a:Arg1> => instructions::Instruction::Bra(instructions::bra::BraData{ uniform: u.is_some() }, a)
};

// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#data-movement-and-conversion-instructions-cvt
InstCvt: instructions::Instruction<instructions::arguments::ParsedArgParams<'input>> = {
    "cvt" <s:".sat"?> <dst_t:CvtTypeInt> <src_t:CvtTypeInt> <a:Arg2> => {
        instructions::Instruction::Cvt(instructions::cvt::CvtDetails::new_int_from_int_checked(
            s.is_some(),
            dst_t,
            src_t,
            errors
        ),
        a)
    },
    "cvt" <r:RoundingModeFloat> <f:".ftz"?> <s:".sat"?> <dst_t:CvtTypeFloat> <src_t:CvtTypeInt> <a:Arg2> => {
        instructions::Instruction::Cvt(instructions::cvt::CvtDetails::new_float_from_int_checked(
            r,
            f.is_some(),
            s.is_some(),
            dst_t,
            src_t,
            errors
        ),
        a)
    },
    "cvt" <r:RoundingModeInt> <f:".ftz"?> <s:".sat"?> <dst_t:CvtTypeInt> <src_t:CvtTypeFloat> <a:Arg2> => {
        instructions::Instruction::Cvt(instructions::cvt::CvtDetails::new_int_from_float_checked(
            r,
            f.is_some(),
            s.is_some(),
            dst_t,
            src_t,
            errors
        ),
        a)
    },
    "cvt" <r:RoundingModeInt?> <s:".sat"?> ".f16" ".f16" <a:Arg2> => {
        instructions::Instruction::Cvt(instructions::cvt::CvtDetails::FloatFromFloat(
            instructions::cvt::CvtDesc {
                rounding: r,
                flush_to_zero: None,
                saturate: s.is_some(),
                dst: types::scalar::FloatType::F16,
                src: types::scalar::FloatType::F16
            }
        ), a)
    },
    "cvt" <f:".ftz"?> <s:".sat"?> ".f32" ".f16" <a:Arg2> => {
        instructions::Instruction::Cvt(instructions::cvt::CvtDetails::FloatFromFloat(
            instructions::cvt::CvtDesc {
                rounding: None,
                flush_to_zero: Some(f.is_some()),
                saturate: s.is_some(),
                dst: types::scalar::FloatType::F32,
                src: types::scalar::FloatType::F16
            }
        ), a)
    },
    "cvt" <s:".sat"?> ".f64" ".f16" <a:Arg2> => {
        instructions::Instruction::Cvt(instructions::cvt::CvtDetails::FloatFromFloat(
            instructions::cvt::CvtDesc {
                rounding: None,
                flush_to_zero: None,
                saturate: s.is_some(),
                dst: types::scalar::FloatType::F64,
                src: types::scalar::FloatType::F16
            }
        ), a)
    },
    "cvt" <r:RoundingModeFloat> <f:".ftz"?> <s:".sat"?> ".f16" ".f32" <a:Arg2> => {
        instructions::Instruction::Cvt(instructions::cvt::CvtDetails::FloatFromFloat(
            instructions::cvt::CvtDesc {
                rounding: Some(r),
                flush_to_zero: Some(f.is_some()),
                saturate: s.is_some(),
                dst: types::scalar::FloatType::F16,
                src: types::scalar::FloatType::F32
            }
        ), a)
    },
    "cvt" <r:RoundingModeInt?> <f:".ftz"?> <s:".sat"?> ".f32" ".f32" <a:Arg2> => {
        instructions::Instruction::Cvt(instructions::cvt::CvtDetails::FloatFromFloat(
            instructions::cvt::CvtDesc {
                rounding: r,
                flush_to_zero: Some(f.is_some()),
                saturate: s.is_some(),
                dst: types::scalar::FloatType::F32,
                src: types::scalar::FloatType::F32
            }
        ), a)
    },
    "cvt" <s:".sat"?> ".f64" ".f32" <a:Arg2> => {
        instructions::Instruction::Cvt(instructions::cvt::CvtDetails::FloatFromFloat(
            instructions::cvt::CvtDesc {
                rounding: None,
                flush_to_zero: None,
                saturate: s.is_some(),
                dst: types::scalar::FloatType::F64,
                src: types::scalar::FloatType::F32
            }
        ), a)
    },
    "cvt" <r:RoundingModeFloat> <s:".sat"?> ".f16" ".f64" <a:Arg2> => {
        instructions::Instruction::Cvt(instructions::cvt::CvtDetails::FloatFromFloat(
            instructions::cvt::CvtDesc {
                rounding: Some(r),
                flush_to_zero: None,
                saturate: s.is_some(),
                dst: types::scalar::FloatType::F16,
                src: types::scalar::FloatType::F64
            }
        ), a)
    },
    "cvt" <r:RoundingModeFloat> <f:".ftz"?> <s:".sat"?> ".f32" ".f64" <a:Arg2> => {
        instructions::Instruction::Cvt(instructions::cvt::CvtDetails::FloatFromFloat(
            instructions::cvt::CvtDesc {
                rounding: Some(r),
                flush_to_zero: Some(s.is_some()),
                saturate: s.is_some(),
                dst: types::scalar::FloatType::F32,
                src: types::scalar::FloatType::F64
            }
        ), a)
    },
    "cvt" <r:RoundingModeInt?> <s:".sat"?> ".f64" ".f64" <a:Arg2> => {
        instructions::Instruction::Cvt(instructions::cvt::CvtDetails::FloatFromFloat(
            instructions::cvt::CvtDesc {
                rounding: r,
                flush_to_zero: None,
                saturate: s.is_some(),
                dst: types::scalar::FloatType::F64,
                src: types::scalar::FloatType::F64
            }
        ), a)
    },
};

CvtTypeInt: types::scalar::IntType = {
    ".u8" => types::scalar::IntType::U8,
    ".u16" => types::scalar::IntType::U16,
    ".u32" => types::scalar::IntType::U32,
    ".u64" => types::scalar::IntType::U64,
    ".s8" => types::scalar::IntType::S8,
    ".s16" => types::scalar::IntType::S16,
    ".s32" => types::scalar::IntType::S32,
    ".s64" => types::scalar::IntType::S64,
};

CvtTypeFloat: types::scalar::FloatType = {
    ".f16" => types::scalar::FloatType::F16,
    ".f32" => types::scalar::FloatType::F32,
    ".f64" => types::scalar::FloatType::F64,
};

// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#logic-and-shift-instructions-shl
InstShl: instructions::Instruction<instructions::arguments::ParsedArgParams<'input>> = {
    "shl" <t:ShlType> <a:Arg3> => instructions::Instruction::Shl(t, a)
};

ShlType: instructions::shl::ShlType = {
    ".b16" => instructions::shl::ShlType::B16,
    ".b32" => instructions::shl::ShlType::B32,
    ".b64" => instructions::shl::ShlType::B64,
};

// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#logic-and-shift-instructions-shr
InstShr: instructions::Instruction<instructions::arguments::ParsedArgParams<'input>> = {
    "shr" <t:ShrType> <a:Arg3> => instructions::Instruction::Shr(t, a)
};

ShrType: instructions::shr::ShrType = {
    ".b16" => instructions::shr::ShrType::B16,
    ".b32" => instructions::shr::ShrType::B32,
    ".b64" => instructions::shr::ShrType::B64,
    ".u16" => instructions::shr::ShrType::U16,
    ".u32" => instructions::shr::ShrType::U32,
    ".u64" => instructions::shr::ShrType::U64,
    ".s16" => instructions::shr::ShrType::S16,
    ".s32" => instructions::shr::ShrType::S32,
    ".s64" => instructions::shr::ShrType::S64,
};

// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#data-movement-and-conversion-instructions-st
// Warning: NVIDIA documentation is incorrect, you can specify scope only once
InstSt: instructions::Instruction<instructions::arguments::ParsedArgParams<'input>> = {
    "st" <q:LdStQualifier?> <ss:StStateSpace?> <cop:StCacheOperator?> <t:LdStType> <src1:MemoryOperand> "," <src2:SrcOperandVec> => {
        instructions::Instruction::St(
            instructions::st::StData {
                qualifier: q.unwrap_or(instructions::ld::LdStQualifier::Weak),
                state_space: ss.unwrap_or(instructions::st::StStateSpace::Generic),
                caching: cop.unwrap_or(instructions::st::StCacheOperator::Writeback),
                typ: t
            },
            instructions::st::Arg2St { src1:src1, src2:src2 }
        )
    }
};

// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#using-addresses-arrays-and-vectors
MemoryOperand: arguments::Operand<&'input str> = {
    "[" <o:Operand> "]" => o
}

StStateSpace: instructions::st::StStateSpace = {
    ".global" => instructions::st::StStateSpace::Global,
    ".local" => instructions::st::StStateSpace::Local,
    ".param" => instructions::st::StStateSpace::Param,
    ".shared" => instructions::st::StStateSpace::Shared,
};

StCacheOperator: instructions::st::StCacheOperator = {
    ".wb" => instructions::st::StCacheOperator::Writeback,
    ".cg" => instructions::st::StCacheOperator::L2Only,
    ".cs" => instructions::st::StCacheOperator::Streaming,
    ".wt" => instructions::st::StCacheOperator::Writethrough,
};

// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#control-flow-instructions-ret
InstRet: instructions::Instruction<instructions::arguments::ParsedArgParams<'input>> = {
    "ret" <u:".uni"?> => instructions::Instruction::Ret(instructions::ret::RetData { uniform: u.is_some() })
};

// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#data-movement-and-conversion-instructions-cvta
InstCvta: instructions::Instruction<instructions::arguments::ParsedArgParams<'input>> = {
    "cvta" <from:CvtaStateSpace> <s:CvtaSize> <a:Arg2> => {
        instructions::Instruction::Cvta(instructions::cvta::CvtaDetails {
            to: instructions::cvta::CvtaStateSpace::Generic,
            from,
            size: s
        },
        a)
    },
    "cvta" ".to" <to:CvtaStateSpace> <s:CvtaSize> <a:Arg2> => {
        instructions::Instruction::Cvta(instructions::cvta::CvtaDetails {
            to,
            from: instructions::cvta::CvtaStateSpace::Generic,
            size: s
        },
        a)
    }
}

CvtaStateSpace: instructions::cvta::CvtaStateSpace = {
    ".const" => instructions::cvta::CvtaStateSpace::Const,
    ".global" => instructions::cvta::CvtaStateSpace::Global,
    ".local" => instructions::cvta::CvtaStateSpace::Local,
    ".shared" => instructions::cvta::CvtaStateSpace::Shared,
}

CvtaSize: instructions::cvta::CvtaSize = {
    ".u32" => instructions::cvta::CvtaSize::U32,
    ".u64" => instructions::cvta::CvtaSize::U64,
}

// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#control-flow-instructions-call
InstCall: instructions::Instruction<instructions::arguments::ParsedArgParams<'input>> = {
    "call" <u:".uni"?> <args:ArgCall> => {
        let (ret_params, func, param_list) = args;
        instructions::Instruction::Call(instructions::call::CallInst { uniform: u.is_some(), ret_params, func, param_list })
    }
};

// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#integer-arithmetic-instructions-abs
InstAbs: instructions::Instruction<instructions::arguments::ParsedArgParams<'input>> = {
    "abs" <t:SignedIntType> <a:Arg2> => {
        instructions::Instruction::Abs(instructions::abs::AbsDetails { flush_to_zero: None, typ: t }, a)
    },
    "abs" <f:".ftz"?> ".f32" <a:Arg2> => {
        instructions::Instruction::Abs(instructions::abs::AbsDetails { flush_to_zero: Some(f.is_some()), typ: types::scalar::ScalarType::F32 }, a)
    },
    "abs" ".f64" <a:Arg2> => {
        instructions::Instruction::Abs(instructions::abs::AbsDetails { flush_to_zero: None, typ: types::scalar::ScalarType::F64 }, a)
    },
    "abs" <f:".ftz"?> ".f16" <a:Arg2> => {
        instructions::Instruction::Abs(instructions::abs::AbsDetails { flush_to_zero: Some(f.is_some()), typ: types::scalar::ScalarType::F16 }, a)
    },
    "abs" <f:".ftz"?> ".f16x2" <a:Arg2> => {
        instructions::Instruction::Abs(instructions::abs::AbsDetails { flush_to_zero: Some(f.is_some()), typ: types::scalar::ScalarType::F16x2 }, a)
    },
};

// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#integer-arithmetic-instructions-mad
// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#floating-point-instructions-mad
InstMad: instructions::Instruction<instructions::arguments::ParsedArgParams<'input>> = {
    "mad" <d:MulDetails> <a:Arg4> => instructions::Instruction::Mad(d, a),
    "mad" ".hi" ".sat" ".s32" => todo!(),
    "fma" <f:ArithFloatMustRound> <a:Arg4> => instructions::Instruction::Mad(instructions::mul::MulDetails::Float(f), a),
};

SignedIntType: types::scalar::ScalarType = {
    ".s16" => types::scalar::ScalarType::S16,
    ".s32" => types::scalar::ScalarType::S32,
    ".s64" => types::scalar::ScalarType::S64,
};

// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#logic-and-shift-instructions-or
InstOr: instructions::Instruction<instructions::arguments::ParsedArgParams<'input>> = {
    "or" <d:BooleanType> <a:Arg3> => instructions::Instruction::Or(d, a),
};

// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#logic-and-shift-instructions-and
InstAnd: instructions::Instruction<instructions::arguments::ParsedArgParams<'input>> = {
    "and" <d:BooleanType> <a:Arg3> => instructions::Instruction::And(d, a),
};

// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#floating-point-instructions-rcp
InstRcp: instructions::Instruction<instructions::arguments::ParsedArgParams<'input>> = {
    "rcp" <rounding:RcpRoundingMode> <ftz:".ftz"?> ".f32" <a:Arg2> => {
        let details = instructions::rcp::RcpDetails {
            rounding,
            flush_to_zero: Some(ftz.is_some()),
            is_f64: false,
        };
        instructions::Instruction::Rcp(details, a)
    },
    "rcp" <rn:RoundingModeFloat> ".f64" <a:Arg2> => {
        let details = instructions::rcp::RcpDetails {
            rounding: Some(rn),
            flush_to_zero: None,
            is_f64: true,
        };
        instructions::Instruction::Rcp(details, a)
    }
};

RcpRoundingMode: Option<instructions::rcp::RoundingMode> = {
    ".approx" => None,
    <r:RoundingModeFloat> => Some(r)
};

// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#integer-arithmetic-instructions-sub
// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#floating-point-instructions-sub
// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#half-precision-floating-point-instructions-sub
InstSub: instructions::Instruction<instructions::arguments::ParsedArgParams<'input>> = {
    "sub" <d:ArithDetails> <a:Arg3> => instructions::Instruction::Sub(d, a),
};

// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#integer-arithmetic-instructions-min
// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#floating-point-instructions-min
// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#half-precision-floating-point-instructions-min
InstMin: instructions::Instruction<instructions::arguments::ParsedArgParams<'input>> = {
    "min" <d:MinMaxDetails> <a:Arg3> => instructions::Instruction::Min(d, a),
};

// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#integer-arithmetic-instructions-max
// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#floating-point-instructions-max
// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#half-precision-floating-point-instructions-max
InstMax: instructions::Instruction<instructions::arguments::ParsedArgParams<'input>> = {
    "max" <d:MinMaxDetails> <a:Arg3> => instructions::Instruction::Max(d, a),
};

MinMaxDetails: instructions::min_max::MinMaxDetails = {
    <t:UIntType> => instructions::min_max::MinMaxDetails::Unsigned(t),
    <t:SIntType> => instructions::min_max::MinMaxDetails::Signed(t),
    <ftz:".ftz"?> <nan:".NaN"?> ".f32" => instructions::min_max::MinMaxDetails::Float(
        instructions::min_max::MinMaxFloat{ flush_to_zero: Some(ftz.is_some()), nan: nan.is_some(), typ: types::scalar::FloatType::F32 }
    ),
    ".f64" => instructions::min_max::MinMaxDetails::Float(
        instructions::min_max::MinMaxFloat{ flush_to_zero: None, nan: false, typ: types::scalar::FloatType::F64 }
    ),
    <ftz:".ftz"?> <nan:".NaN"?> ".f16" => instructions::min_max::MinMaxDetails::Float(
        instructions::min_max::MinMaxFloat{ flush_to_zero: Some(ftz.is_some()), nan: nan.is_some(), typ: types::scalar::FloatType::F16 }
    ),
    <ftz:".ftz"?> <nan:".NaN"?> ".f16x2" => instructions::min_max::MinMaxDetails::Float(
        instructions::min_max::MinMaxFloat{ flush_to_zero: Some(ftz.is_some()), nan: nan.is_some(), typ: types::scalar::FloatType::F16x2 }
    )
}

// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#comparison-and-selection-instructions-selp
InstSelp: instructions::Instruction<instructions::arguments::ParsedArgParams<'input>> = {
    "selp" <t:SelpType> <a:Arg4> => instructions::Instruction::Selp(t, a),
};

SelpType: types::scalar::SelpType = {
    ".b16" => types::scalar::SelpType::B16,
    ".b32" => types::scalar::SelpType::B32,
    ".b64" => types::scalar::SelpType::B64,
    ".u16" => types::scalar::SelpType::U16,
    ".u32" => types::scalar::SelpType::U32,
    ".u64" => types::scalar::SelpType::U64,
    ".s16" => types::scalar::SelpType::S16,
    ".s32" => types::scalar::SelpType::S32,
    ".s64" => types::scalar::SelpType::S64,
    ".f32" => types::scalar::SelpType::F32,
    ".f64" => types::scalar::SelpType::F64,
};

// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#parallel-synchronization-and-communication-instructions-bar
InstBar: instructions::Instruction<instructions::arguments::ParsedArgParams<'input>> = {
    "barrier" ".sync" ".aligned" <a:Arg1Bar> => instructions::Instruction::Bar(instructions::bar::BarDetails::SyncAligned, a),
    "bar" ".sync" <a:Arg1Bar> => instructions::Instruction::Bar(instructions::bar::BarDetails::SyncAligned, a)
}

// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#parallel-synchronization-and-communication-instructions-atom
// The documentation does not mention all spported operations:
// * Operation .add requires .u32 or .s32 or .u64 or .f64 or f16 or f16x2 or .f32
// * Operation .inc requires .u32 type for instuction
// * Operation .dec requires .u32 type for instuction
// Otherwise as documented
InstAtom: instructions::Instruction<instructions::arguments::ParsedArgParams<'input>> = {
    "atom" <sema:AtomSemantics?> <scope:MemScope?> <space:AtomSpace?> <op:AtomBitOp> <typ:BitType> <a:Arg3Atom> => {
        let details = instructions::atoms::AtomDetails {
            semantics: sema.unwrap_or(instructions::atoms::AtomSemantics::Relaxed),
            scope: scope.unwrap_or(instructions::ld::MemScope::Gpu),
            space: space.unwrap_or(instructions::atoms::AtomSpace::Generic),
            inner: instructions::atoms::AtomInnerDetails::Bit { op, typ }
        };
        instructions::Instruction::Atom(details,a)
    },
    "atom" <sema:AtomSemantics?> <scope:MemScope?> <space:AtomSpace?> ".inc" ".u32" <a:Arg3Atom> => {
        let details = instructions::atoms::AtomDetails {
            semantics: sema.unwrap_or(instructions::atoms::AtomSemantics::Relaxed),
            scope: scope.unwrap_or(instructions::ld::MemScope::Gpu),
            space: space.unwrap_or(instructions::atoms::AtomSpace::Generic),
            inner: instructions::atoms::AtomInnerDetails::Unsigned {
                op: instructions::atoms::AtomUIntOp::Inc,
                typ: types::scalar::UIntType::U32
            }
        };
        instructions::Instruction::Atom(details,a)
    },
    "atom" <sema:AtomSemantics?> <scope:MemScope?> <space:AtomSpace?> ".dec" ".u32" <a:Arg3Atom> => {
        let details = instructions::atoms::AtomDetails {
            semantics: sema.unwrap_or(instructions::atoms::AtomSemantics::Relaxed),
            scope: scope.unwrap_or(instructions::ld::MemScope::Gpu),
            space: space.unwrap_or(instructions::atoms::AtomSpace::Generic),
            inner: instructions::atoms::AtomInnerDetails::Unsigned {
                op: instructions::atoms::AtomUIntOp::Dec,
                typ: types::scalar::UIntType::U32
            }
        };
        instructions::Instruction::Atom(details,a)
    },
    "atom" <sema:AtomSemantics?> <scope:MemScope?> <space:AtomSpace?> ".add" <typ:FloatType> <a:Arg3Atom> => {
        let op = instructions::atoms::AtomFloatOp::Add;
        let details = instructions::atoms::AtomDetails {
            semantics: sema.unwrap_or(instructions::atoms::AtomSemantics::Relaxed),
            scope: scope.unwrap_or(instructions::ld::MemScope::Gpu),
            space: space.unwrap_or(instructions::atoms::AtomSpace::Generic),
            inner: instructions::atoms::AtomInnerDetails::Float { op, typ }
        };
        instructions::Instruction::Atom(details,a)
    },
    "atom" <sema:AtomSemantics?> <scope:MemScope?> <space:AtomSpace?> <op: AtomUIntOp> <typ:UIntType3264> <a:Arg3Atom> => {
        let details = instructions::atoms::AtomDetails {
            semantics: sema.unwrap_or(instructions::atoms::AtomSemantics::Relaxed),
            scope: scope.unwrap_or(instructions::ld::MemScope::Gpu),
            space: space.unwrap_or(instructions::atoms::AtomSpace::Generic),
            inner: instructions::atoms::AtomInnerDetails::Unsigned { op, typ }
        };
        instructions::Instruction::Atom(details,a)
    },
    "atom" <sema:AtomSemantics?> <scope:MemScope?> <space:AtomSpace?> <op: AtomSIntOp> <typ:SIntType3264> <a:Arg3Atom> => {
        let details = instructions::atoms::AtomDetails {
            semantics: sema.unwrap_or(instructions::atoms::AtomSemantics::Relaxed),
            scope: scope.unwrap_or(instructions::ld::MemScope::Gpu),
            space: space.unwrap_or(instructions::atoms::AtomSpace::Generic),
            inner: instructions::atoms::AtomInnerDetails::Signed { op, typ }
        };
        instructions::Instruction::Atom(details,a)
    }
}

InstAtomCas: instructions::Instruction<instructions::arguments::ParsedArgParams<'input>> = {
    "atom" <sema:AtomSemantics?> <scope:MemScope?> <space:AtomSpace?> ".cas" <typ:BitType> <a:Arg4Atom> => {
        let details = instructions::atoms::AtomCasDetails {
            semantics: sema.unwrap_or(instructions::atoms::AtomSemantics::Relaxed),
            scope: scope.unwrap_or(instructions::ld::MemScope::Gpu),
            space: space.unwrap_or(instructions::atoms::AtomSpace::Generic),
            typ,
        };
        instructions::Instruction::AtomCas(details,a)
    },
}

AtomSemantics: instructions::atoms::AtomSemantics = {
    ".relaxed" => instructions::atoms::AtomSemantics::Relaxed,
    ".acquire" => instructions::atoms::AtomSemantics::Acquire,
    ".release" => instructions::atoms::AtomSemantics::Release,
    ".acq_rel" => instructions::atoms::AtomSemantics::AcquireRelease
}

AtomSpace: instructions::atoms::AtomSpace = {
    ".global" => instructions::atoms::AtomSpace::Global,
    ".shared" => instructions::atoms::AtomSpace::Shared
}

AtomBitOp: instructions::atoms::AtomBitOp = {
    ".and" => instructions::atoms::AtomBitOp::And,
    ".or" => instructions::atoms::AtomBitOp::Or,
    ".xor" => instructions::atoms::AtomBitOp::Xor,
    ".exch" => instructions::atoms::AtomBitOp::Exchange,
}

AtomUIntOp: instructions::atoms::AtomUIntOp = {
    ".add" => instructions::atoms::AtomUIntOp::Add,
    ".min" => instructions::atoms::AtomUIntOp::Min,
    ".max" => instructions::atoms::AtomUIntOp::Max,
}

AtomSIntOp: instructions::atoms::AtomSIntOp = {
    ".add" => instructions::atoms::AtomSIntOp::Add,
    ".min" => instructions::atoms::AtomSIntOp::Min,
    ".max" => instructions::atoms::AtomSIntOp::Max,
}

BitType: types::scalar::BitType = {
    ".b32" => types::scalar::BitType::B32,
    ".b64" => types::scalar::BitType::B64,
}

UIntType3264: types::scalar::UIntType = {
    ".u32" => types::scalar::UIntType::U32,
    ".u64" => types::scalar::UIntType::U64,
}

SIntType3264: types::scalar::SIntType = {
    ".s32" => types::scalar::SIntType::S32,
    ".s64" => types::scalar::SIntType::S64,
}

// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#floating-point-instructions-div
// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#integer-arithmetic-instructions-div
InstDiv: instructions::Instruction<instructions::arguments::ParsedArgParams<'input>> = {
    "div" <t:UIntType> <a:Arg3> => instructions::Instruction::Div(instructions::div::DivDetails::Unsigned(t), a),
    "div" <t:SIntType> <a:Arg3> => instructions::Instruction::Div(instructions::div::DivDetails::Signed(t), a),
    "div" <kind:DivFloatKind> <ftz:".ftz"?> ".f32" <a:Arg3> => {
        let inner = instructions::div::DivFloatDetails {
            typ: types::scalar::FloatType::F32,
            flush_to_zero: Some(ftz.is_some()),
            kind
        };
        instructions::Instruction::Div(instructions::div::DivDetails::Float(inner), a)
    },
    "div" <rnd:RoundingModeFloat> ".f64" <a:Arg3> => {
        let inner = instructions::div::DivFloatDetails {
            typ: types::scalar::FloatType::F64,
            flush_to_zero: None,
            kind: instructions::div::DivFloatKind::Rounding(rnd)
        };
        instructions::Instruction::Div(instructions::div::DivDetails::Float(inner), a)
    },
}

DivFloatKind: instructions::div::DivFloatKind = {
    ".approx" => instructions::div::DivFloatKind::Approx,
    ".full" => instructions::div::DivFloatKind::Full,
    <rnd:RoundingModeFloat> => instructions::div::DivFloatKind::Rounding(rnd),
}

// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#floating-point-instructions-sqrt
InstSqrt: instructions::Instruction<instructions::arguments::ParsedArgParams<'input>> = {
    "sqrt" ".approx" <ftz:".ftz"?> ".f32" <a:Arg2> => {
        let details = instructions::sqrt::SqrtDetails {
            typ: types::scalar::FloatType::F32,
            flush_to_zero: Some(ftz.is_some()),
            kind: instructions::sqrt::SqrtKind::Approx,
        };
        instructions::Instruction::Sqrt(details, a)
    },
    "sqrt" <rnd:RoundingModeFloat> <ftz:".ftz"?> ".f32" <a:Arg2> => {
        let details = instructions::sqrt::SqrtDetails {
            typ: types::scalar::FloatType::F32,
            flush_to_zero: Some(ftz.is_some()),
            kind: instructions::sqrt::SqrtKind::Rounding(rnd),
        };
        instructions::Instruction::Sqrt(details, a)
    },
    "sqrt" <rnd:RoundingModeFloat> ".f64" <a:Arg2> => {
        let details = instructions::sqrt::SqrtDetails {
            typ: types::scalar::FloatType::F64,
            flush_to_zero: None,
            kind: instructions::sqrt::SqrtKind::Rounding(rnd),
        };
        instructions::Instruction::Sqrt(details, a)
    }
}

// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#floating-point-instructions-rsqrt
// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#floating-point-instructions-rsqrt-approx-ftz-f64
InstRsqrt: instructions::Instruction<instructions::arguments::ParsedArgParams<'input>> = {
    "rsqrt" ".approx" <ftz:".ftz"?> ".f32" <a:Arg2> => {
        let details = instructions::rsqrt::RsqrtDetails {
            typ: types::scalar::FloatType::F32,
            flush_to_zero: ftz.is_some(),
        };
        instructions::Instruction::Rsqrt(details, a)
    },
    "rsqrt" ".approx" <ftz:".ftz"?> ".f64" <a:Arg2> => {
        let details = instructions::rsqrt::RsqrtDetails {
            typ: types::scalar::FloatType::F64,
            flush_to_zero: ftz.is_some(),
        };
        instructions::Instruction::Rsqrt(details, a)
    },
}

// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#integer-arithmetic-instructions-neg
// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#floating-point-instructions-neg
// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#half-precision-floating-point-instructions-neg
InstNeg: instructions::Instruction<instructions::arguments::ParsedArgParams<'input>> = {
    "neg" <ftz:".ftz"?> <typ:NegTypeFtz> <a:Arg2> => {
        let details = instructions::neg::NegDetails {
            typ,
            flush_to_zero: Some(ftz.is_some()),
        };
        instructions::Instruction::Neg(details, a)
    },
    "neg" <typ:NegTypeNonFtz> <a:Arg2> => {
        let details = instructions::neg::NegDetails {
            typ,
            flush_to_zero: None,
        };
        instructions::Instruction::Neg(details, a)
    },
}

// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#floating-point-instructions-sin
InstSin: instructions::Instruction<instructions::arguments::ParsedArgParams<'input>> = {
    "sin" ".approx" <ftz:".ftz"?> ".f32" <arg:Arg2> => {
        instructions::Instruction::Sin{ flush_to_zero: ftz.is_some(), arg }
    },
}

// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#floating-point-instructions-cos
InstCos: instructions::Instruction<instructions::arguments::ParsedArgParams<'input>> = {
    "cos" ".approx" <ftz:".ftz"?> ".f32" <arg:Arg2> => {
        instructions::Instruction::Cos{ flush_to_zero: ftz.is_some(), arg }
    },
}

// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#floating-point-instructions-lg2
InstLg2: instructions::Instruction<instructions::arguments::ParsedArgParams<'input>> = {
    "lg2" ".approx" <ftz:".ftz"?> ".f32" <arg:Arg2> => {
        instructions::Instruction::Lg2{ flush_to_zero: ftz.is_some(), arg }
    },
}

// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#floating-point-instructions-ex2
InstEx2: instructions::Instruction<instructions::arguments::ParsedArgParams<'input>> = {
    "ex2" ".approx" <ftz:".ftz"?> ".f32" <arg:Arg2> => {
        instructions::Instruction::Ex2{ flush_to_zero: ftz.is_some(), arg }
    },
}

// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#integer-arithmetic-instructions-clz
InstClz: instructions::Instruction<instructions::arguments::ParsedArgParams<'input>> = {
    "clz" <typ:BitType> <arg:Arg2> => instructions::Instruction::Clz{ <> }
}

// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#integer-arithmetic-instructions-brev
InstBrev: instructions::Instruction<instructions::arguments::ParsedArgParams<'input>> = {
    "brev" <typ:BitType> <arg:Arg2> => instructions::Instruction::Brev{ <> }
}

// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#integer-arithmetic-instructions-popc
InstPopc: instructions::Instruction<instructions::arguments::ParsedArgParams<'input>> = {
    "popc" <typ:BitType> <arg:Arg2> => instructions::Instruction::Popc{ <> }
}

// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#logic-and-shift-instructions-xor
InstXor: instructions::Instruction<instructions::arguments::ParsedArgParams<'input>> = {
    "xor" <typ:BooleanType> <arg:Arg3> => instructions::Instruction::Xor{ <> }
}

// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#integer-arithmetic-instructions-bfe
InstBfe: instructions::Instruction<instructions::arguments::ParsedArgParams<'input>> = {
    "bfe" <typ:IntType3264> <arg:Arg4> => instructions::Instruction::Bfe{ <> }
}

// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#integer-arithmetic-instructions-rem
InstRem: instructions::Instruction<instructions::arguments::ParsedArgParams<'input>> = {
    "rem" <typ:IntType> <arg:Arg3> => instructions::Instruction::Rem{ <> }
}


NegTypeFtz: types::scalar::ScalarType = {
    ".f16" => types::scalar::ScalarType::F16,
    ".f16x2" => types::scalar::ScalarType::F16x2,
    ".f32" => types::scalar::ScalarType::F32,
}

NegTypeNonFtz: types::scalar::ScalarType = {
    ".s16" => types::scalar::ScalarType::S16,
    ".s32" => types::scalar::ScalarType::S32,
    ".s64" => types::scalar::ScalarType::S64,
    ".f64" => types::scalar::ScalarType::F64
}

ArithDetails: instructions::add::ArithDetails = {
    <t:UIntType> => instructions::add::ArithDetails::Unsigned(t),
    <t:SIntType> => instructions::add::ArithDetails::Signed(instructions::add::ArithSInt {
        typ: t,
        saturate: false,
    }),
    ".sat" ".s32" => instructions::add::ArithDetails::Signed(instructions::add::ArithSInt {
        typ: types::scalar::SIntType::S32,
        saturate: true,
    }),
    <f:ArithFloat> => instructions::add::ArithDetails::Float(f)
}

ArithFloat: instructions::add::ArithFloat = {
    <rn:RoundingModeFloat?> <ftz:".ftz"?> <sat:".sat"?> ".f32" => instructions::add::ArithFloat {
        typ: types::scalar::FloatType::F32,
        rounding: rn,
        flush_to_zero: Some(ftz.is_some()),
        saturate: sat.is_some(),
    },
    <rn:RoundingModeFloat?> ".f64" => instructions::add::ArithFloat {
        typ: types::scalar::FloatType::F64,
        rounding: rn,
        flush_to_zero: None,
        saturate: false,
    },
    <rn:".rn"?> <ftz:".ftz"?> <sat:".sat"?> ".f16" => instructions::add::ArithFloat {
        typ: types::scalar::FloatType::F16,
        rounding: rn.map(|_| instructions::rcp::RoundingMode::NearestEven),
        flush_to_zero: Some(ftz.is_some()),
        saturate: sat.is_some(),
    },
    <rn:".rn"?> <ftz:".ftz"?> <sat:".sat"?> ".f16x2" => instructions::add::ArithFloat {
        typ: types::scalar::FloatType::F16x2,
        rounding: rn.map(|_| instructions::rcp::RoundingMode::NearestEven),
        flush_to_zero: Some(ftz.is_some()),
        saturate: sat.is_some(),
    },
}

ArithFloatMustRound: instructions::add::ArithFloat = {
    <rn:RoundingModeFloat> <ftz:".ftz"?> <sat:".sat"?> ".f32" => instructions::add::ArithFloat {
        typ: types::scalar::FloatType::F32,
        rounding: Some(rn),
        flush_to_zero: Some(ftz.is_some()),
        saturate: sat.is_some(),
    },
    <rn:RoundingModeFloat> ".f64" => instructions::add::ArithFloat {
        typ: types::scalar::FloatType::F64,
        rounding: Some(rn),
        flush_to_zero: None,
        saturate: false,
    },
    ".rn" <ftz:".ftz"?> <sat:".sat"?> ".f16" => instructions::add::ArithFloat {
        typ: types::scalar::FloatType::F16,
        rounding: Some(instructions::rcp::RoundingMode::NearestEven),
        flush_to_zero: Some(ftz.is_some()),
        saturate: sat.is_some(),
    },
    ".rn" <ftz:".ftz"?> <sat:".sat"?> ".f16x2" => instructions::add::ArithFloat {
        typ: types::scalar::FloatType::F16x2,
        rounding: Some(instructions::rcp::RoundingMode::NearestEven),
        flush_to_zero: Some(ftz.is_some()),
        saturate: sat.is_some(),
    },
}

Operand: arguments::Operand<&'input str> = {
    <r:ExtendedID> => arguments::Operand::Reg(r),
    <r:ExtendedID> "+" <offset:S32Num> => arguments::Operand::RegOffset(r, offset),
    <x:ImmediateValue> => arguments::Operand::Imm(x)
};

CallOperand: arguments::Operand<&'input str> = {
    <r:ExtendedID> => arguments::Operand::Reg(r),
    <x:ImmediateValue> => arguments::Operand::Imm(x)
};

// TODO: start parsing whole constants sub-language:
//       https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#constants
ImmediateValue: instructions::arguments::ImmediateValue = {
    // TODO: treat negation correctly
    <neg:"-"?> <x:NumToken> =>? {
        let (num, radix, is_unsigned) = x;
        if neg.is_some() {
            match i64::from_str_radix(num, radix) {
                Ok(x) => Ok(instructions::arguments::ImmediateValue::S64(-x)),
                Err(err) => Err(ParseError::User { error: error::PtxError::ParseInt(err) })
            }
        } else if is_unsigned {
            match u64::from_str_radix(num, radix) {
                Ok(x) => Ok(instructions::arguments::ImmediateValue::U64(x)),
                Err(err) => Err(ParseError::User { error: error::PtxError::ParseInt(err) })
            }
        } else {
            match i64::from_str_radix(num, radix) {
                Ok(x) => Ok(instructions::arguments::ImmediateValue::S64(x)),
                Err(_) => {
                    match u64::from_str_radix(num, radix) {
                        Ok(x) => Ok(instructions::arguments::ImmediateValue::U64(x)),
                        Err(err) => Err(ParseError::User { error: error::PtxError::ParseInt(err) })
                    }
                }
            }
        }
    },
    <f:F32Num> => {
        instructions::arguments::ImmediateValue::F32(f)
    },
    <f:F64Num> => {
        instructions::arguments::ImmediateValue::F64(f)
    }
}

Arg1: arguments::Arg1<instructions::arguments::ParsedArgParams<'input>> = {
    <src:ExtendedID> => arguments::Arg1{<>}
};

Arg1Bar: arguments::Arg1Bar<instructions::arguments::ParsedArgParams<'input>> = {
    <src:Operand> => arguments::Arg1Bar{<>}
};

Arg2: instructions::arguments::Arg2<instructions::arguments::ParsedArgParams<'input>> = {
    <dst:DstOperand> "," <src:Operand> => instructions::arguments::Arg2{<>}
};

MemberOperand: (&'input str, u8) = {
    <pref:ExtendedID> "." <suf:ExtendedID> =>? {
        let suf_idx = vector_index(suf)?;
        Ok((pref, suf_idx))
    },
    <pref:ExtendedID> <suf:DotID>  =>? {
        let suf_idx = vector_index(&suf[1..])?;
        Ok((pref, suf_idx))
    }
};

VectorExtract: Vec<&'input str> = {
    "{" <r1:ExtendedID> "," <r2:ExtendedID> "}" => {
        vec![r1, r2]
    },
    "{" <r1:ExtendedID> "," <r2:ExtendedID> "," <r3:ExtendedID> "," <r4:ExtendedID> "}" => {
        vec![r1, r2, r3, r4]
    },
};

Arg3: instructions::arguments::Arg3<instructions::arguments::ParsedArgParams<'input>> = {
    <dst:DstOperand> "," <src1:Operand> "," <src2:Operand> => instructions::arguments::Arg3{<>}
};

Arg3Atom: instructions::arguments::Arg3<instructions::arguments::ParsedArgParams<'input>> = {
    <dst:DstOperand> "," "[" <src1:Operand> "]" "," <src2:Operand> => instructions::arguments::Arg3{<>}
};

Arg4: instructions::arguments::Arg4<instructions::arguments::ParsedArgParams<'input>> = {
    <dst:DstOperand> "," <src1:Operand> "," <src2:Operand> ","  <src3:Operand> => instructions::arguments::Arg4{<>}
};

Arg4Atom: instructions::arguments::Arg4<instructions::arguments::ParsedArgParams<'input>> = {
    <dst:DstOperand> "," "[" <src1:Operand> "]" "," <src2:Operand> ","  <src3:Operand> => instructions::arguments::Arg4{<>}
};

Arg4Setp: instructions::arguments::Arg4Setp<instructions::arguments::ParsedArgParams<'input>> = {
    <dst1:ExtendedID> <dst2:OptionalDst?> "," <src1:Operand> "," <src2:Operand> => instructions::arguments::Arg4Setp{<>}
};

// TODO: pass src3 negation somewhere
Arg5Setp: instructions::arguments::Arg5Setp<instructions::arguments::ParsedArgParams<'input>> = {
    <dst1:ExtendedID> <dst2:OptionalDst?> "," <src1:Operand> "," <src2:Operand> "," "!"? <src3:Operand> => instructions::arguments::Arg5Setp{<>}
};

ArgCall: (Vec<&'input str>, &'input str, Vec<arguments::Operand<&'input str>>) = {
    "(" <ret_params:Comma<ExtendedID>> ")" "," <func:ExtendedID> "," "(" <param_list:Comma<CallOperand>> ")" => {
        (ret_params, func, param_list)
    },
    <func:ExtendedID> "," "(" <param_list:Comma<CallOperand>> ")" => (Vec::new(), func, param_list),
    <func:ExtendedID> => (Vec::new(), func, Vec::<arguments::Operand<_>>::new()),
};

OptionalDst: &'input str = {
    "|" <dst2:ExtendedID> => dst2
}

SrcOperand: arguments::Operand<&'input str> = {
    <r:ExtendedID> => arguments::Operand::Reg(r),
    <r:ExtendedID> "+" <offset:S32Num> => arguments::Operand::RegOffset(r, offset),
    <x:ImmediateValue> => arguments::Operand::Imm(x),
    <mem_op:MemberOperand> => {
        let (reg, idx) = mem_op;
        arguments::Operand::VecMember(reg, idx)
    }
}

SrcOperandVec: arguments::Operand<&'input str> = {
    <normal:SrcOperand> => normal,
    <vec:VectorExtract> => arguments::Operand::VecPack(vec),
}

DstOperand: arguments::Operand<&'input str> = {
    <r:ExtendedID> => arguments::Operand::Reg(r),
    <mem_op:MemberOperand> => {
        let (reg, idx) = mem_op;
        arguments::Operand::VecMember(reg, idx)
    }
}

DstOperandVec: arguments::Operand<&'input str> = {
    <normal:DstOperand> => normal,
    <vec:VectorExtract> => arguments::Operand::VecPack(vec),
}

VectorPrefix: u8 = {
    ".v2" => 2,
    ".v4" => 4
};

// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#debugging-directives-file
File = {
    ".file" U32Num String ("," U32Num "," U32Num)?
};

// https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#debugging-directives-section
Section = {
    ".section" DotID "{" SectionDwarfLines* "}"
};

SectionDwarfLines: () = {
    AnyBitType Comma<U32Num>,
    ".b32" SectionLabel,
    ".b64" SectionLabel,
    ".b32" SectionLabel "+" U32Num,
    ".b64" SectionLabel "+" U32Num,
};

SectionLabel = {
    ID,
    DotID
};

AnyBitType = {
    ".b8", ".b16", ".b32", ".b64"
};

VariableScalar<T>: (Option<u32>, T, &'input str) = {
    <align:Align?> <v_type:T> <name:ExtendedID> => {
        (align, v_type, name)
    }
}

VariableVector<T>: (Option<u32>, u8, T, &'input str) = {
    <align:Align?> <v_len:VectorPrefix> <v_type:T> <name:ExtendedID> => {
        (align, v_len, v_type, name)
    }
}

// empty dimensions [0] means it's a pointer
VariableArrayOrPointer<T>: (Option<u32>, T, &'input str, ast::ArrayOrPointer) = {
    <align:Align?> <typ:SizedScalarType> <name:ExtendedID> <dims:ArrayDimensions> <init:ArrayInitializer?> =>? {
        let mut dims = dims;
        let array_init = match init {
            Some(init) => {
                let init_vec = init.to_vec(typ, &mut dims)?;
                ast::ArrayOrPointer::Array { dimensions: dims, init: init_vec }
            }
            None => {
                if dims.len() > 1 && dims.contains(&0) {
                    return Err(ParseError::User { error: error::PtxError::ZeroDimensionArray })
                }
                match &*dims {
                    [0] => ast::ArrayOrPointer::Pointer,
                    _ => ast::ArrayOrPointer::Array { dimensions: dims, init: Vec::new() }
                }
            }
        };
        Ok((align, typ, name, array_init))
    }
}

// [0] and [] are treated the same
ArrayDimensions: Vec<u32> = {
    ArrayEmptyDimension => vec![0u32],
    ArrayEmptyDimension <dims:ArrayDimension+> => {
        let mut dims = dims;
        let mut result = vec![0u32];
        result.append(&mut dims);
        result
    },
    <dims:ArrayDimension+> => dims
}

ArrayEmptyDimension = {
    "[" "]"
}

ArrayDimension: u32  = {
    "[" <n:U32Num> "]" => n,
}

ArrayInitializer: ast::NumsOrArrays<'input> = {
    "=" <nums:NumsOrArraysBracket> => nums
}

NumsOrArraysBracket: ast::NumsOrArrays<'input> = {
    "{" <nums:NumsOrArrays> "}" => nums
}

NumsOrArrays: ast::NumsOrArrays<'input> = {
    <n:Comma<NumsOrArraysBracket>> => ast::NumsOrArrays::Arrays(n),
    <n:CommaNonEmpty<NumToken>> => ast::NumsOrArrays::Nums(n.into_iter().map(|(x,radix,_)| (x, radix)).collect()),
}

Comma<T>: Vec<T> = {
    <v:(<T> ",")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            let mut v = v;
            v.push(e);
            v
        }
    }
};

CommaNonEmpty<T>: Vec<T> = {
    <v:(<T> ",")*> <e:T> => {
        let mut v = v;
        v.push(e);
        v
    }
};

#[inline]
Or<T1, T2>: T1 = {
    T1,
    T2
}
